PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX devices: <https://renew.iccs.gr/projects/41837b47-5e2f-4e25-973b-ae74980b7ea8/devices/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX waterkwaliteit: <https://data.vlaanderen.be/ns/waterkwaliteit#>
PREFIX units: <https://si-digital-framework.org/SI/units/>
PREFIX schema: <https://schema.org/>
PREFIX terms:  <http://purl.org/dc/terms/>

CONSTRUCT {
  ?observation rdf:type sosa:Observation.
  ?observation sosa:madeBySensor ?sensor.
  ?observation terms:isVersionOf ?sensor.
  ?observation waterkwaliteit:locatie ?finalLocation.
  ?observation prov:generatedAtTime ?generatedTime.
  ?observation qudt:numericValue ?finalNumericValue.
  ?observation qudt:value ?finalStringValue.
  ?observation sosa:observedProperty _:blankNode.
  _:blankNode rdfs:label ?label.
  _:blankNode qudt:siExactMatch ?finalUnit.
  _:blankNode qudt:hasQuantityKind ?quantityKind.
  _:blankNode schema:additionalProperty ?additionalProperty.
}
WHERE {
  # Define fixed values
  # Ghent be61a56a-038b-44dc-86f2-8c319739efc8
  # Location 51°06'12.5"N 3°43'37.3"E, ‘Marina Zelzate’. URL is
  BIND("be61a56a-038b-44dc-86f2-8c319739efc8" AS ?sensorID)
  BIND("51°12'28.9\"N 3°48'19.3\"E" AS ?defaultLocation)

  # Define variables
  BIND(IRI(CONCAT(STR(devices:), ?sensorID)) AS ?sensor)
  BIND(IRI(CONCAT(STR(?sensor), "/", STR(NOW()))) AS ?observation)
  BIND(IRI(CONCAT(STR(?observation), "/read")) AS ?read)

  # Ensure ?subject is defined somewhere in the dataset
  ?subject prov:generatedAtTime ?generatedTime.
  ?subject sosa:observedProperty ?nextedResult.
  ?nextedResult rdfs:label ?label.
  ?nextedResult schema:additionalProperty ?additionalProperty.
  ?nextedResult qudt:hasQuantityKind ?quantityKind.

  # Handle optional location binding
  OPTIONAL { ?subject waterkwaliteit:locatie ?location. }
  BIND(COALESCE(?location, ?defaultLocation) AS ?finalLocation)

  # Handle optional numeric and string values
  OPTIONAL { ?subject qudt:numericValue ?numericValue. }
  BIND(COALESCE(?numericValue, rdf:nil) AS ?finalNumericValue)

  OPTIONAL { ?subject qudt:value ?stringValue. }
  BIND(COALESCE(?stringValue, rdf:nil) AS ?finalStringValue)

  OPTIONAL { ?nextedResult qudt:siExactMatch ?unit. }
  BIND(COALESCE(?stringValue, rdf:nil) AS ?processedUnit)

  # Handle unit mapping using units prefix
  BIND(
    IF(?processedUnit = "C", units:degreeCelsius,
      IF(?processedUnit = "V", units:volt,
        IF(?processedUnit = "m", units:meter,
          ?processedUnit  # Default: keep original if no match
        )
      )
    ) AS ?finalUnit
  )
}
